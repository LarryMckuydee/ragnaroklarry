//===== rAthena Script =======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6b
//===== Compatible With: =====================================
//= rAthena SVN r16862+
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//= 1.6b Added 'disable_items' command.
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
xmas_in,170,26,6	script	Quest DonationEQ#1	998,{ callfunc "runqshopDonationEQ"; }


// Script Core
//============================================================
-	script	quest_shopDonationEQ	-1,{ 
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";


// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

	/*Add(5022,1,0,0,7086,1,969,10,999,40,1003,50,984,2);
	Add(5032,1,0,0,1059,250,2221,1,2227,1,7063,600);
	Add(5027,1,0,0,2252,1,1036,400,7001,50,4052,1);
	Add(5045,1,0,0,2252,1,1054,450,943,1200);*/
	
//Add(5086,1,0,0,1095,1000,2288,1);// Alarm Mask
//Add(5389,1,10000000,0,5074,1,5043,1,999,100);//,10000000zeny Angel Spirit

//UPPER
Add(5826,1,30000000,0,5373,1,7510,20,7023,20,7511,300,7566,10,7340,50);//zeny Valkyrie Helmet 20
Add(5312,1,0,0,2261,1,7094,100,7211,20);// Ayothaya King's Hat [1] 5 
Add(5137,1,50000000,0,2290,1,5161,1,7047,100,4253,10,7277,300);//zeny Alice Doll[1] 15
Add(5857,1,30000000,0,5161,1,1030,30,1029,20,7036,20,7003,200,7107,100,7161,100);//zeny Cool FB Hat 15
Add(5308,1,20000000,0,5147,1,5016,1,7562,5,968,30,7294,10,7291,10);//zeny  Brazil National Flag Hat[1] 15
Add(5278,1,0,0,5192,5,2215,1,2210,1,969,20,5164,1,985,100,671,3);// Yellow Ribbon 15
Add(5393,1,20000000,0,5041,1,2210,1,7213,500,994,100);//zeny Love Valentine's Hat 10
Add(5805,1,50000000,0,2249,1,5161,1,5162,1,969,30,7754,1);//zeny Victorious Coronet 25
Add(5396,1,15000000,0,5364,1,5133,1,5311,1,7451,10);//zeny Jasper Crest[1] 15
Add(5423,1,10000000,0,5293,1,5393,1);//zeny I Love China 20
Add(5293,1,30000000,0,2274,1,7290,10,607,30,12395,30);//zeny Ramen Hat 15
Add(5376,1,30000000,0,5373,1,5068,1,5074,1,1039,2000,7563,100,7511,350);//zeny Flying Evil Wing[1] 25
Add(5374,1,50000000,0,5160,1,5161,1,923,1000,750,30,1038,500,7160,1000);//zeny Gigantic Majestic Goat 30
Add(5518,1,50000000,0,5160,1,5161,1,923,1000,750,30,1038,500,7160,1000);//zeny Gigantic Majestic Goat 30
Add(5353,1,10000000,0,7086,1,5053,1,969,20);//zeny Hat of the SUn God[1] 35

//MIDDLE

Add(5314,1,3000000,0,5005,1,7095,200,7319,1000);//zeny Hockey Mask 10
Add(5565,1,5000000,0,5314,1,1035,300,7265,300,7123,200,7451,10);//zeny Dragon Arhat Mask 25
Add(5135,1,2000000,0,5006,1,2295,1,7563,200);//zeny Cyclop's Eye 5 
Add(5800,1,3000000,0,5040,1,5102,1,7165,500);//zeny Blush of Gloom 10
Add(5610,1,10000000,0,5074,1,7562,10);//zeny Ice Wing Ear 10
Add(5421,1,50000000,0,5074,1,7798,200,7799,200);//zeny Ears of Ifrit 10
Add(5325,1,20000000,0,5135,1,7300,20);//zeny Robo Eye 10
Add(5470,1,20000000,0,5135,1,5104,1);//zeny Eye of Darkness 10
Add(18507,1,5000000,0,2286,1,7292,100);//zeny Elven Ears[1] 20
Add(2204,1,5000000,0,2260,1,7290,100);//zeny Glasses[1] 20
Add(2202,1,5000000,0,2201,1,7297,100);//zeny Sunglasses[1] 20

//LOWER


Add(5305,1,20000000,0,1476,1,999,50);//zeny Pirate Dagger 5
Add(5361,1,1000000,0,7216,700);//zeny Gangster Scarf 5
Add(5775,1,20000000,0,5445,1,529,100,530,200,538,300);//zeny Choco Donut in Mouth 10
Add(5419,1,3000000,0,2270,3,7100,1000,945,200);//zeny Leaves of Grass 10
Add(5594,1,20000000,0,5445,1,529,100,530,200,538,300);//zeny Donut In Mouth 10
Add(5156,1,10000000,0,2218,1,7063,300);//zeny Father's Mask 10
Add(5362,1,3000000,0,7433,500,618,600);//zeny Ninja Scroll 10
Add(5377,1,3000000,0,2268,1,7850,3000);//zeny Gentleman's Pipe 15
Add(5463,1,0,0,5361,1,7446,200,7445,200,7447,200,7448,200);// Rainbow Scarf 15
Add(5462,1,0,0,5361,1,7106,300,1021,300,7265,100,923,150);// Spiked Scarf 15
Add(5155,1,10000000,0,5156,1,2240,1,1040,500,7754,1);//zeny Father's White Moustache 15



// -----------------------------------------------------------

	npcshopdelitem "qshopDonationEQ",909;
	end;

OnMenu:
	dispbottom "Select one item at a time.";
	callshop "qshopDonationEQ",1;
	npcshopattach "qshopDonationEQ";
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	disable_items;
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) { 
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.Announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(0)) == "null") {
		debugmes "Quest reward #"+getarg(0)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(1),getarg(2),getarg(3);
	for(set .@i,4; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		}else{ //Add(531,1,3,0,512,1,713,1);
			setarray .@j[.@i-1],getarg(.@i),getarg(.@i+1);
		}
	}
	copyarray getd(".q_"+getarg(0)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshopDonationEQ",getarg(0),((.ShowZeny)?getarg(2):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	runqshopDonationEQ	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shopDonationEQ::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshopDonationEQ	-1,909:-1
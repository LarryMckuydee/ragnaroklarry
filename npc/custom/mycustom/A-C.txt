//===== rAthena Script =======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6b
//===== Compatible With: =====================================
//= rAthena SVN r16862+
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//= 1.6b Added 'disable_items' command.
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
xmas,129,125,6	script	Quest A-C#1	998,{ callfunc "runqshopA-C"; }


// Script Core
//============================================================
-	script	quest_shopA-C	-1,{ 
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";


// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

	/*Add(5022,1,0,0,7086,1,969,10,999,40,1003,50,984,2);
	Add(5032,1,0,0,1059,250,2221,1,2227,1,7063,600);
	Add(5027,1,0,0,2252,1,1036,400,7001,50,4052,1);
	Add(5045,1,0,0,2252,1,1054,450,943,1200);*/
	
Add(5086,1,0,0,1095,1000,2288,1);// Alarm Mask
Add(5207,1,0,0,2282,1,2610,10,2611,10);// Angel Blessing
Add(5389,1,10000000,0,5074,1,5043,1,999,100);//,10000000zeny Angel Spirit
Add(5074,1,200000,0,2254,1,2286,1);//,200000zeny Angel Wing Ears
Add(5153,1,1000000,0,5079,1,741,10);//,1000000zeny Angeling Hairpin
Add(5132,1,3000000,0,5025,1,5035,1);//,3000000zeny Angeling Hat
Add(5486,1,0,0,2236,1,1001,200,978,3,7063,10);// Anniversary Hat
Add(2284,1,0,0,923,20);// Antlers
Add(5490,1,0,0,1045,500,4138,3,999,150);// Anubis Helm
Add(5513,1,0,0,7798,200,7799,200,5165,1);// Aquaris Crown
Add(5512,1,10000000,0,5512,1);//,10000000zeny Aquaris Diadem
Add(5546,1,0,0,7798,200,7799,200,5165,1);//  Aries Crown
Add(5545,1,10000000,0,5546,1);//,10000000zeny Aries Diadem
Add(5505,1,0,0,5003,1,7063,30,982,10);// Asara Fairy Hat
Add(5189,1,0,0,521,30,7198,20);// Autumn Leaves
Add(5174,1,0,0,7445,7,7446,7,7447,7,7448,7,954,150,975,1,733,5,7166,50);// Ayam[1]
Add(5110,1,0,0,526,2,7270,1,941,1,10004,1);// Baby Pacifier
Add(5147,1,0,0,5349,1,978,1,982,1);// Baseball Cap[1]
Add(5076,1,0,0,2226,1,7038,500);// Beanie
Add(5416,1,0,0,970,20,999,50);// Beer Hat
Add(5382,1,0,0,5083,1,5195,1,5091,1);// Bell Ribbon [1]
Add(5338,1,0,0,2290,1,7850,300,982,1);// BF Recruiter Hat
Add(5480,1,0,0,5071,1,7036,10,999,50,985,100,984,100,7101,30);// Bijofnil Wings
Add(2296,1,20000,0,2243,1,999,20);//,20000zeny Binoculars
Add(5129,1,0,0,925,100,916,200);// Bird Nest
Add(5183,1,0,0,5129,1,9005,2);// Bird Nest Hat
Add(5057,1,10000,0,2213,1,983,1,914,200);//,10000zeny Black Cat Ears
Add(5401,1,5000000,0,5047,1,983,3,969,3);//,5000000zeny Black Frame Glasses
Add(5191,1,0,0,2244,1,2209,1,983,2);// Black Ribbon[1]
Add(5102,1,0,0,2239,1,7354,30,7355,30,999,50,568,30);// Blank Eyes
Add(5065,1,0,0,1023,30);// Blue Fish
Add(5052,1,0,0,2211,1,978,1,7003,10);// Blue Hairband
Add(5540,1,0,0,7047,100);// Blush
Add(5016,1,0,0,1030,10);// Boy's Cap
Add(5169,1,0,0,706,1,7201,50,1049,20,1059,500,1024,50);// Bride Mask[1]
Add(5439,1,0,0,745,1,2338,1,2206,1);// Bride's Corolla
Add(5410,1,0,0,7151,30);// Brown Paperbag Hat
Add(5034,1,0,0,2233,1,746,20);// Bulb Band
Add(5300,1,0,0,2299,1,5017,1);// Bullock Helm
Add(2214,1,0,0,949,30,706,1,722,1,2213,1);// Bunny Band
Add(5378,1,0,0,2214,2,2227,2);// Bunny Top Hat
Add(5024,1,0,0,529,10,530,5,539,20,999,10,538,15);// Cake Hat
Add(5414,1,0,0,7068,50,7049,50);// Camp Fire Hat
Add(5582,1,0,0,7798,200,7799,200,5165,1);// Cancer Crown
Add(5581,1,0,0,5582,1);//,10000000zeny Cancer Diadem
Add(5028,1,0,0,2279,1,7035,50,526,100);// Candle
Add(5445,1,1000000,0,530,100,529,100,2267,1);//,1000000 Candy Cane in Mouth
Add(5304,1,0,0,2502,1,12020,20,7511,25);// Cap of Blindness[1]
Add(5744,1,0,0,7798,200,7799,200,5165,1);// Capricon Crown[1]
Add(5745,1,0,0,5744,1);//,10000000zeny Capricon Diadem
Add(5184,1,0,0,5016,1,720,30);// Captain's Hat
Add(5307,1,0,0,5116,1,514,50,7164,60);// Carmen Miranda's Hat
Add(5446,1,0,0,2747,1,5077,1);// Catfoot Hairpin
Add(5175,1,0,0,2276,1,7355,30);// Censor Bar
Add(5140,1,0,0,5083,1,5195,1,7213,50);// Charming Ribbon[1]
Add(5026,1,0,0,1036,450,949,330,982,1,539,120);// Chef Hat
Add(5283,1,0,0,925,100,949,150,916,150,7115,100,7101,100,7063,30,976,5);// Chick Hat
Add(5431,1,20000000,0,5283,1,982,5);//,20000000zeny Chicken Hat[1]
Add(5609,1,0,0,5042,1,5311,1,5092,1,975,5);// Chung Hairband
Add(5131,1,0,0,2233,1,2229,1,999,20,998,50);// Close Helmet
Add(5260,1,2000000,0,538,150,4235,1,4293,1);//2000000zeny Cookie Hat
Add(5200,1,500000,0,5016,1,981,1,982,1);//,500000zeny Coppola
Add(5075,1,0,0,2248,1,7030,108,7194,108,7120,4);// Cowboy Hat
Add(5142,1,0,0,5048,1,2229,1,999,30);// Crescent Helm
Add(5048,1,0,0,5041,1,999,10);// Crescent Hairpin
Add(5036,1,0,0,2233,1,7069,100);// Cross Hat
Add(5081,1,50000,0,2249,1,714,1,969,1);//,50000 zeny Crown of Mistress
Add(5417,1,0,0,5431,1,5002,1);// Crown Parrots
Add(5107,1,0,0,519,50,7031,50,548,50,539,50);// Crunch Toast


// -----------------------------------------------------------

	npcshopdelitem "qshopA-C",909;
	end;

OnMenu:
	dispbottom "Select one item at a time.";
	callshop "qshopA-C",1;
	npcshopattach "qshopA-C";
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	disable_items;
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) { 
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.Announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(0)) == "null") {
		debugmes "Quest reward #"+getarg(0)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(1),getarg(2),getarg(3);
	for(set .@i,4; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		}else{ //Add(531,1,3,0,512,1,713,1);
			setarray .@j[.@i-1],getarg(.@i),getarg(.@i+1);
		}
	}
	copyarray getd(".q_"+getarg(0)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshopA-C",getarg(0),((.ShowZeny)?getarg(2):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	runqshopA-C	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shopA-C::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshopA-C	-1,909:-1